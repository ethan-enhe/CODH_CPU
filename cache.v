/*
2è·¯ç»„ç›¸è”Cache
- Cacheè¡Œæ•°ï¼?64è¡?
- å—å¤§å°ï¼š16å­—èŠ‚(512bit)
- LRU
*/
module cache #(
    parameter INDEX_WIDTH       = 12,    // Cacheç´¢å¼•ä½å®½ 8k
    parameter LINE_OFFSET_WIDTH = 0,    // è¡Œåç§»ä½å®½ï¼Œå†³å®šäº†ä¸€è¡Œçš„å®½åº¦ 1word
    parameter SPACE_OFFSET      = 2    // ä¸?ä¸ªåœ°å?ç©ºé—´å?1ä¸ªå­—èŠ‚ï¼Œå› æ­¤ä¸?ä¸ªå­—éœ?è¦?4ä¸ªåœ°å?ç©ºé—´ï¼Œç”±äºå‡è®¾ä¸ºæ•´å­—è¯»å–ï¼Œå¤„ç†åœ°å?çš„æ—¶å€™å¯ä»¥é»˜è®¤åä¸¤ä½ä¸?0
)(
    input                     clk,    
    input                     rstn,
    /* CPUæ¥å£ */  
    input [31:0]              addr,   // CPUåœ°å€
    input                     r_req,  // CPUè¯»è¯·æ±?
    input                     w_req,  // CPUå†™è¯·æ±?
    input [31:0]              w_data,  // CPUå†™æ•°æ?
    output [31:0]             r_data,  // CPUè¯»æ•°æ?
    output reg                miss,   // ç¼“å­˜æœªå‘½ä¸?
    /* å†…å­˜æ¥å£ */  
    output reg                     mem_r,  // å†…å­˜è¯»è¯·æ±?
    output reg                     mem_w,  // å†…å­˜å†™è¯·æ±?
    output reg [31:0]              mem_addr,  // å†…å­˜åœ°å€
    output reg [31:0] mem_w_data,  // å†…å­˜å†™æ•°æ? ä¸?æ¬¡å†™ä¸?è¡?
    input      [31:0] mem_r_data,  // å†…å­˜è¯»æ•°æ? ä¸?æ¬¡è¯»ä¸?è¡?
    input                          mem_ready  // å†…å­˜å°±ç»ªä¿¡å·
);

    // Cacheå‚æ•°
    localparam
        WAY_NUM = 2,
        // Cacheè¡Œå®½åº?
        LINE_WIDTH = 32 << LINE_OFFSET_WIDTH,
        // æ ‡è®°ä½å®½åº?
        TAG_WIDTH = 32 - INDEX_WIDTH - LINE_OFFSET_WIDTH - SPACE_OFFSET,
        // Cacheè¡Œæ•°
        SET_NUM   = 1 << INDEX_WIDTH;
    
    // Cacheç›¸å…³å¯„å­˜å™?
    reg [31:0]           addr_buf;    // è¯·æ±‚åœ°å€ç¼“å­˜-ç”¨äºä¿ç•™CPUè¯·æ±‚åœ°å€
    reg [31:0]           w_data_buf;  // å†™æ•°æ®ç¼“å­?
    reg op_buf;                       // è¯»å†™æ“ä½œç¼“å­˜ï¼Œç”¨äºåœ¨MISSçŠ¶æ?ä¸‹åˆ¤æ–­æ˜¯è¯»è¿˜æ˜¯å†™ï¼Œå¦‚æœæ˜¯å†™åˆ™éœ€è¦å°†æ•°æ®å†™å›å†…å­˜ 0:è¯? 1:å†?
    reg [LINE_WIDTH-1:0] ret_buf;     // è¿”å›æ•°æ®ç¼“å­˜-ç”¨äºä¿ç•™å†…å­˜è¿”å›æ•°æ®

    // Cacheå¯¼çº¿
    wire [INDEX_WIDTH-1:0] r_index;  // ç´¢å¼•è¯»åœ°å?
    wire [INDEX_WIDTH-1:0] w_index;  // ç´¢å¼•å†™åœ°å?
    wire [LINE_WIDTH-1:0]  r_line;   // Data Bramæœ?ç»ˆçš„è¯»æ•°æ?(ç»è¿‡muxé€‰æ‹©åçš„æ•°æ®)
    wire  [LINE_WIDTH-1:0]  r_line_tmp[WAY_NUM-1:0];  // Bramè¯»æ•°æ?
    wire [LINE_WIDTH-1:0]  w_line;   // Data Bramå†™æ•°æ?
    wire [LINE_WIDTH-1:0]  w_line_mask;  // Data Bramå†™æ•°æ®æ©ç ?
    wire [LINE_WIDTH-1:0]  w_data_line;  // è¾“å…¥å†™æ•°æ®ç§»ä½åçš„æ•°æ?
    wire [TAG_WIDTH-1:0]   tag;      // CPUè¯·æ±‚åœ°å€ä¸­åˆ†ç¦»çš„æ ‡è®° ç”¨äºæ¯”è¾ƒ ä¹Ÿå¯ç”¨äºå†™å…¥

    wire [TAG_WIDTH-1:0]   r_tag_tmp[WAY_NUM-1:0];    // Tag Bramè¯»æ•°æ? ç”¨äºæ¯”è¾ƒ
    wire r_valid_tmp[WAY_NUM-1:0];  // Tag Bramè¯»æ•°æ? ç”¨äºæ¯”è¾ƒ
    wire r_dirty_tmp[WAY_NUM-1:0];  // Tag Bramè¯»æ•°æ? ç”¨äºæ¯”è¾ƒ

    wire [TAG_WIDTH-1:0]   r_tag;    // Tag Bramè¯»æ•°æ? ç”¨äºæ¯”è¾ƒ
    //wire [LINE_OFFSET_WIDTH-1:0] word_offset;  // å­—åç§?
    wire [LINE_OFFSET_WIDTH:0] word_offset;
    reg  [31:0]            cache_data;  // Cacheæ•°æ®
    reg  [31:0]            mem_data;    // å†…å­˜æ•°æ®
    wire [31:0]            dirty_mem_addr; // é€šè¿‡è¯»å‡ºçš„tagå’Œå¯¹åº”çš„indexï¼Œåç§»ç­‰å¾—åˆ°è„å—å¯¹åº”çš„å†…å­˜åœ°å?å¹¶å†™å›åˆ°æ­£ç¡®çš„ä½ç½?
    wire valid;  // Cacheæœ‰æ•ˆä½?
    wire dirty;  // Cacheè„ä½.
    reg  w_valid;  // Cacheå†™æœ‰æ•ˆä½
    reg  w_dirty;  // Cacheå†™è„ä½?
    //reg  w_tag;  // Cacheå†™æ ‡è®°ä½
    wire hit;    // Cacheå‘½ä¸­
    wire hit_way;  // Cacheå‘½ä¸­çš„way
    reg hit_way_buf;
    // reg hit_wat_buf_we;

    // Cacheç›¸å…³æ§åˆ¶ä¿¡å·
    reg addr_buf_we;  // è¯·æ±‚åœ°å€ç¼“å­˜å†™ä½¿èƒ?
    reg ret_buf_we;   // è¿”å›æ•°æ®ç¼“å­˜å†™ä½¿èƒ?
    reg data_we[WAY_NUM-1:0];      // Cacheå†™ä½¿èƒ?
    reg tag_we[WAY_NUM-1:0];       // Cacheæ ‡è®°å†™ä½¿èƒ?
    reg data_from_mem;  // ä»å†…å­˜è¯»å–æ•°æ?
    reg refill;       // æ ‡è®°éœ?è¦é‡æ–°å¡«å……ï¼Œåœ¨MISSçŠ¶æ?ä¸‹æ¥å—åˆ°å†…å­˜æ•°æ®åç½?1,åœ¨IDLEçŠ¶æ?ä¸‹è¿›è¡Œå¡«å……åç½®0


    //ageç›¸å…³æ§åˆ¶ä¿¡å·
    reg age_we;
    wire r_age;

    // çŠ¶æ?æœºä¿¡å·
    localparam 
        IDLE      = 3'd0,  // ç©ºé—²çŠ¶æ??
        READ      = 3'd1,  // è¯»çŠ¶æ€?
        MISS      = 3'd2,  // ç¼ºå¤±æ—¶ç­‰å¾…ä¸»å­˜è¯»å‡ºæ–°å?
        WRITE     = 3'd3,  // å†™çŠ¶æ€?
        W_DIRTY   = 3'd4;  // å†™ç¼ºå¤±æ—¶ç­‰å¾…ä¸»å­˜å†™å…¥è„å—
    reg [2:0] CS;  // çŠ¶æ?æœºå½“å‰çŠ¶æ??
    reg [2:0] NS;  // çŠ¶æ?æœºä¸‹ä¸€çŠ¶æ??

    // çŠ¶æ?æœº
    always @(posedge clk or negedge rstn) begin
        if (!rstn) begin
            CS <= IDLE;
        end else begin
            CS <= NS;
        end
    end

    // ä¸­é—´å¯„å­˜å™¨ä¿ç•™åˆå§‹çš„è¯·æ±‚åœ°å€å’Œå†™æ•°æ®ï¼Œå¯ä»¥ç†è§£ä¸ºaddr_bufä¸­çš„åœ°å€ä¸ºå½“å‰Cacheæ­£åœ¨å¤„ç†çš„è¯·æ±‚åœ°å?ï¼Œè?Œaddrä¸­çš„åœ°å€ä¸ºæ–°çš„è¯·æ±‚åœ°å?
    always @(posedge clk or negedge rstn) begin
        if (!rstn) begin
            addr_buf <= 0;
            ret_buf <= 0;
            w_data_buf <= 0;
            op_buf <= 0;
            refill <= 0;
        end else begin
            if (addr_buf_we) begin
                addr_buf <= addr;
                w_data_buf <= w_data;
                op_buf <= w_req;
            end
            if (ret_buf_we) begin
                ret_buf <= mem_r_data;
            end
            if (CS == MISS && mem_ready) begin
                refill <= 1;
            end
            if (CS == IDLE) begin
                refill <= 0;
            end
        end
    end

    // å¯¹è¾“å…¥åœ°å?è¿›è¡Œè§£ç 
    assign r_index = addr[INDEX_WIDTH+LINE_OFFSET_WIDTH+SPACE_OFFSET - 1: LINE_OFFSET_WIDTH+SPACE_OFFSET];
    assign w_index = addr_buf[INDEX_WIDTH+LINE_OFFSET_WIDTH+SPACE_OFFSET - 1: LINE_OFFSET_WIDTH+SPACE_OFFSET];
    assign tag = addr_buf[31:INDEX_WIDTH+LINE_OFFSET_WIDTH+SPACE_OFFSET];
  //  assign word_offset = addr_buf[LINE_OFFSET_WIDTH+SPACE_OFFSET-1:SPACE_OFFSET];
      assign word_offset = 0;

    // è„å—åœ°å€è®¡ç®—
    assign dirty_mem_addr = {r_tag, w_index}<<(LINE_OFFSET_WIDTH+SPACE_OFFSET);

    // å†™å›åœ°å€ã€æ•°æ®å¯„å­˜å™¨
    reg [31:0] dirty_mem_addr_buf;
    reg [31:0] dirty_mem_data_buf;
    always @(posedge clk or negedge rstn) begin
        if (!rstn) begin
            dirty_mem_addr_buf <= 0;
            dirty_mem_data_buf <= 0;
            hit_way_buf <= 0;
        end else begin
            if (CS == READ || CS == WRITE) begin
                dirty_mem_addr_buf <= dirty_mem_addr;
                dirty_mem_data_buf <= r_line;
                hit_way_buf <= hit_way;
            end
        end
    end

    //Age Bram
    bram #(
        .ADDR_WIDTH(INDEX_WIDTH),
        .DATA_WIDTH(1)
    ) age_bram1(
        .clk(clk),
        .raddr(r_index),
        .waddr(w_index),
        .din(hit_way),
        .we(age_we),
        .dout(r_age)
    );

    // Tag Bram
    bram #(
        .ADDR_WIDTH(INDEX_WIDTH),
        .DATA_WIDTH(TAG_WIDTH + 2) // æœ?é«˜ä½ä¸ºæœ‰æ•ˆä½ï¼Œæ¬¡é«˜ä½ä¸ºè„ä½?,å†æ¬¡ä¸?ä½æ˜¯è®¿é—®æ¬¡æ•°ï¼Œä½ä½ä¸ºæ ‡è®°ä½?
    ) tag_bram1(
        .clk(clk),
        .raddr(r_index),
        .waddr(w_index),
        .din({w_valid, w_dirty,tag}),
        .we(tag_we[0]),
        .dout({r_valid_tmp[0], r_dirty_tmp[0], r_tag_tmp[0]})
    );

    

    // Data Bram
    bram #(
        .ADDR_WIDTH(INDEX_WIDTH),
        .DATA_WIDTH(LINE_WIDTH)
    ) data_bram1(
        .clk(clk),
        .raddr(r_index),
        .waddr(w_index),
        .din(w_line),
        .we(data_we[0]),
        .dout(r_line_tmp[0])
    );


    // Tag Bram
    bram #(
        .ADDR_WIDTH(INDEX_WIDTH),
        .DATA_WIDTH(TAG_WIDTH + 2) // æœ?é«˜ä½ä¸ºæœ‰æ•ˆä½ï¼Œæ¬¡é«˜ä½ä¸ºè„ä½ï¼Œä½ä½ä¸ºæ ‡è®°ä½
    ) tag_bram2(
        .clk(clk),
        .raddr(r_index),
        .waddr(w_index),
        .din({w_valid, w_dirty,tag}),
        .we(tag_we[1]),
        .dout({r_valid_tmp[1], r_dirty_tmp[1], r_tag_tmp[1]})
    );

    // Data Bram
    bram #(
        .ADDR_WIDTH(INDEX_WIDTH),
        .DATA_WIDTH(LINE_WIDTH)
    ) data_bram2(
       .clk(clk),
        .raddr(r_index),
        .waddr(w_index),
        .din(w_line),
        .we(data_we[1]),
        .dout(r_line_tmp[1])
    );

    // åˆ¤å®šCacheæ˜¯å¦å‘½ä¸­
    assign hit = ((r_valid_tmp[0] && r_tag_tmp[0] == tag) || (r_valid_tmp[1] && r_tag_tmp[1] == tag));
    assign hit_way=hit?(r_valid_tmp[1] && r_tag_tmp[1] == tag):~r_age;//å‘½ä¸­çš„æ—¶å€™è¡¨ç¤ºå‘½ä¸­çš„wayï¼Œå¦åˆ™è¡¨ç¤ºæ›¿æ¢çš„way
    assign {valid, dirty, r_tag} = {r_valid_tmp[hit_way], r_dirty_tmp[hit_way], r_tag_tmp[hit_way]};
    assign r_line = r_line_tmp[hit_way];

    // å†™å…¥Cache è¿™é‡Œè¦åˆ¤æ–­æ˜¯å‘½ä¸­åå†™å…¥è¿˜æ˜¯æœªå‘½ä¸­åå†™å…?
    assign w_line_mask = 32'hFFFFFFFF << (word_offset*32);   // å†™å…¥æ•°æ®æ©ç 
    assign w_data_line = w_data_buf << (word_offset*32);     // å†™å…¥æ•°æ®ç§»ä½
    assign w_line = (CS == IDLE && op_buf) ? ret_buf & ~w_line_mask | w_data_line : // å†™å…¥æœªå‘½ä¸­ï¼Œéœ?è¦å°†å†…å­˜æ•°æ®ä¸å†™å…¥æ•°æ®åˆå¹?
                    (CS == IDLE) ? ret_buf : // è¯»å–æœªå‘½ä¸?
                    r_line & ~w_line_mask | w_data_line; // å†™å…¥å‘½ä¸­,éœ?è¦å¯¹è¯»å–çš„æ•°æ®ä¸å†™å…¥çš„æ•°æ®è¿›è¡Œåˆå¹?

    // é€‰æ‹©è¾“å‡ºæ•°æ® ä»Cacheæˆ–è?…ä»å†…å­˜ è¿™é‡Œçš„é?‰æ‹©ä¸è¡Œå¤§å°æœ‰å…³ï¼Œå› æ­¤å¦‚æœä½ è°ƒæ•´äº†è¡Œåç§»ä½å®½ï¼Œè¿™é‡Œä¹Ÿéœ?è¦è°ƒæ•?
    always @(*) begin
        cache_data = (r_line>>(word_offset*32))&32'hFFFFFFFF;
        mem_data = (ret_buf>>(word_offset*32))&32'hFFFFFFFF;
    end

    assign r_data = data_from_mem ? mem_data : hit ? cache_data : 0;

    // çŠ¶æ?æœºæ›´æ–°é€»è¾‘
    always @(*) begin
        case(CS)
            IDLE: begin
                if (r_req) begin
                    NS = READ;
                end else if (w_req) begin
                    NS = WRITE;
                end else begin
                    NS = IDLE;
                end
            end
            READ: begin
                if (miss && !dirty) begin
                    NS = MISS;
                end else if (miss && dirty) begin
                    NS = W_DIRTY;
                end else if (r_req) begin
                    NS = READ;
                end else if (w_req) begin
                    NS = WRITE;
                end else begin
                    NS = IDLE;
                end
            end
            MISS: begin
                if (mem_ready) begin // è¿™é‡Œå›åˆ°IDLEçš„åŸå› æ˜¯ä¸ºäº†å»¶è¿Ÿä¸?å‘¨æœŸï¼Œç­‰å¾…ä¸»å­˜è¯»å‡ºçš„æ–°å—å†™å…¥Cacheä¸­çš„å¯¹åº”ä½ç½®
                    NS = IDLE;
                end else begin
                    NS = MISS;
                end
            end
            WRITE: begin
                if (miss && !dirty) begin
                    NS = MISS;
                end else if (miss && dirty) begin
                    NS = W_DIRTY;
                end else if (r_req) begin
                    NS = READ;
                end else if (w_req) begin
                    NS = WRITE;
                end else begin
                    NS = IDLE;
                end
            end
            W_DIRTY: begin
                if (mem_ready) begin  // å†™å®Œè„å—åå›åˆ°MISSçŠ¶æ?ç­‰å¾…ä¸»å­˜è¯»å‡ºæ–°å?
                    NS = MISS;
                end else begin
                    NS = W_DIRTY;
                end
            end
            default: begin
                NS = IDLE;
            end
        endcase
    end

    // çŠ¶æ?æœºæ§åˆ¶ä¿¡å·
    always @(*) begin
        addr_buf_we   = 1'b0;
        ret_buf_we    = 1'b0;
        // hit_way_buf_we = 1'b0;
        data_we[0]    = 1'b0;
        data_we[1]    = 1'b0;
        tag_we[0]     = 1'b0;
        tag_we[1]     = 1'b0;
        w_valid       = 1'b0;
        w_dirty       = 1'b0;
        data_from_mem = 1'b0;
        miss          = 1'b0;
        mem_r         = 1'b0;
        mem_w         = 1'b0;
        mem_addr      = 32'b0;
        mem_w_data    = 0;
        age_we = 1'b0;
        case(CS)
            IDLE: begin
                addr_buf_we = 1'b1; // è¯·æ±‚åœ°å€ç¼“å­˜å†™ä½¿èƒ?
                miss = 1'b0;
                ret_buf_we = 1'b0;
                if(refill) begin
                    data_from_mem = 1'b1;
                    w_valid = 1'b1;
                    w_dirty = 1'b0;
                    data_we[hit_way_buf] = 1'b1;
                    tag_we[hit_way_buf] = 1'b1;
                    if (op_buf) begin // å†?
                        w_dirty = 1'b1;
                    end 
                end
            end
            READ: begin
                data_from_mem = 1'b0;
                if (hit) begin // å‘½ä¸­
                    miss = 1'b0;
                    addr_buf_we = 1'b1; // è¯·æ±‚åœ°å€ç¼“å­˜å†™ä½¿èƒ?
                    age_we = 1'b1;
                end else begin // æœªå‘½ä¸?
                    miss = 1'b1;
                    addr_buf_we = 1'b0; 
                    if (dirty) begin // è„æ•°æ®éœ€è¦å†™å›?
                        mem_w = 1'b1;
                        mem_addr = dirty_mem_addr;
                        mem_w_data = r_line; // å†™å›æ•°æ®
                    end
                end
            end
            MISS: begin
                miss = 1'b1;
                mem_r = 1'b1;
                mem_addr = addr_buf;
                if (mem_ready) begin
                    mem_r = 1'b0;
                    ret_buf_we = 1'b1;
                end 
            end
            WRITE: begin
                data_from_mem = 1'b0;
                if (hit) begin // å‘½ä¸­
                    miss = 1'b0;
                    addr_buf_we = 1'b1; // è¯·æ±‚åœ°å€ç¼“å­˜å†™ä½¿èƒ?
                    w_valid = 1'b1;
                    w_dirty = 1'b1;
                    data_we[hit_way] = 1'b1;
                    tag_we[hit_way] = 1'b1;
                    age_we = 1'b1;
                end else begin // æœªå‘½ä¸?
                    miss = 1'b1;
                    addr_buf_we = 1'b0; 
                    if (dirty) begin // è„æ•°æ®éœ€è¦å†™å›?
                        mem_w = 1'b1;
                        mem_addr = dirty_mem_addr;
                        mem_w_data = r_line; // å†™å›æ•°æ®
                    end
                end
            end
            W_DIRTY: begin
                miss = 1'b1;
                mem_w = 1'b1;
                mem_addr = dirty_mem_addr_buf;
                mem_w_data = dirty_mem_data_buf;
                if (mem_ready) begin
                    mem_w = 1'b0;
                end
            end
            default:;
        endcase
    end

endmodule


